/*
 * This software is in the public domain under CC0 1.0 Universal plus a
 * Grant of Patent License.
 *
 * To the extent possible under law, the author(s) have dedicated all
 * copyright and related and neighboring rights to this software to the
 * public domain worldwide. This software is distributed without any
 * warranty.
 *
 * You should have received a copy of the CC0 Public Domain Dedication
 * along with this software (see the LICENSE.md file). If not, see
 * <http://creativecommons.org/publicdomain/zero/1.0/>.
 */

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked"
}

// Leaving this commented out, REALLY slows things down (about 10x in Java 1.7.0_71 and 1.7.0_75, about 6x in 1.8.0_40, all on Mac)
// tasks.withType(GroovyCompile) { groovyOptions.optimizationOptions.indy = true }

apply plugin: 'groovy'
apply plugin: 'war'

// to run use "gradle dependencyUpdates"
apply plugin: 'com.github.ben-manes.versions'
buildscript {
  repositories { jcenter() }
  dependencies { classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3' }
}

sourceCompatibility = '1.7'
archivesBaseName = 'moqui'

sourceSets {
    start
    api
}

// maybe in the future: repositories { mavenCentral() }
// NOTE: packages should still match group, name, version found at http://mvnrepository.com
repositories {
    flatDir name: 'localLib', dirs: projectDir.absolutePath + '/lib'
    flatDir name: 'localLibCamel', dirs: projectDir.absolutePath + '/lib/camel'
    flatDir name: 'localLibFop', dirs: projectDir.absolutePath + '/lib/fop'
    flatDir name: 'localLibSearch', dirs: projectDir.absolutePath + '/lib/search'
    flatDir name: 'localLibKie', dirs: projectDir.absolutePath + '/lib/kie'
    flatDir name: 'localLibOrientDb', dirs: projectDir.absolutePath + '/lib/orientdb'
}

dependencies {
    compile group: 'org.owasp.antisamy', name: 'antisamy', version: '1.5.3' // New BSD
    compile group: 'org.owasp.esapi', name: 'esapi-moqui', version: '2.1.0' // New BSD

    compile module(group: 'com.atomikos', name: 'transactions', version: '4.0.0M4') { // Apache 2.0
        dependencies module(group: 'com.atomikos', name: 'atomikos-util', version: '4.0.0M4') } // Apache 2.0
    compile group: 'com.atomikos', name: 'transactions-api', version: '4.0.0M4' // Apache 2.0
    compile group: 'com.atomikos', name: 'transactions-jdbc', version: '4.0.0M4' // Apache 2.0
    // may use in the future: runtime group: 'com.atomikos', name: 'transactions-jms', version: '4.0.0M4' // Apache 2.0
    compile group: 'com.atomikos', name: 'transactions-jta', version: '4.0.0M4' // Apache 2.0

    // this is an optional alternative to Atomikos
    compile group: 'org.codehaus.btm', name: 'btm', version: '3.0.0-SNAPSHOT' // Apache 2.0

    apiCompile group: 'org.apache.camel', name: 'camel-core', version: '2.16.0' // Apache 2.0
    compile group: 'org.apache.camel', name: 'camel-core', version: '2.16.0' // Apache 2.0

    compile group: 'commons-codec', name: 'commons-codec', version: '1.10' // Apache 2.0
    compile group: 'org.apache.commons', name: 'commons-collections', version: '3.2.1' // Apache 2.0
    compile group: 'org.apache.commons', name: 'commons-csv', version: '1.2' // Apache 2.0
    compile group: 'org.apache.commons', name: 'commons-email', version: '1.4' // Apache 2.0
    compile group: 'commons-fileupload', name: 'commons-fileupload', version: '1.3.1' // Apache 2.0
    compile group: 'org.apache.commons', name: 'commons-io', version: '2.2' // Apache 2.0
    compile group: 'commons-logging', name: 'commons-logging', version: '1.2' // Apache 2.0
    compile group: 'commons-validator', name: 'commons-validator', version: '1.4.1' // Apache 2.0
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.1' // Apache 2.0
    compile group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.4.3' // Apache 2.0

    compile name: 'cups4j', version: '0.6.4' // LGPL <== NOTE!

    compile group: 'net.sf.ehcache', name: 'ehcache', version: '2.10.1' // Apache 2.0
    compile group: 'org.freemarker', name: 'freemarker', version: '2.3.23' // Apache 2.0

    compile group: 'org.apache.geronimo.specs', name: 'geronimo-j2ee-connector_1.5_spec', version: '' // Apache 2.0
    compile group: 'org.apache.geronimo.specs', name: 'geronimo-jms_1.1_spec', version: '1.1.1' // Apache 2.0
    apiCompile group: 'org.apache.geronimo.specs', name: 'geronimo-jta_1.1_spec', version: '1.1.1' // Apache 2.0
    compile group: 'org.apache.geronimo.specs', name: 'geronimo-jta_1.1_spec', version: '1.1.1' // Apache 2.0
    apiCompile group: 'org.apache.geronimo.specs', name: 'geronimo-servlet_2.5_spec', version: '1.2' // Apache 2.0
    providedCompile group: 'org.apache.geronimo.specs', name: 'geronimo-servlet_2.5_spec', version: '1.2' // Apache 2.0

    apiCompile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.5-indy' // Apache 2.0
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.5-indy' // Apache 2.0

    compile group: 'org.apache.jackrabbit', name: 'jackrabbit-api', version: '2.11.1' // Apache 2.0
    compile group: 'org.apache.jackrabbit', name: 'jackrabbit-jcr-commons', version: '2.11.1' // Apache 2.0
    compile group: 'org.apache.jackrabbit', name: 'jackrabbit-jcr2dav', version: '2.11.1' // Apache 2.0
    compile group: 'org.apache.jackrabbit', name: 'jackrabbit-jcr-rmi', version: '2.11.1' // Apache 2.0
    compile group: 'org.apache.jackrabbit', name: 'jackrabbit-spi', version: '2.11.1' // Apache 2.0
    compile group: 'org.apache.jackrabbit', name: 'jackrabbit-jcr2spi', version: '2.11.1' // Apache 2.0

    compile group: 'com.sun.mail', name: 'javax.mail', version: '1.5.4' // CDDL
    compile group: 'javax.jcr', name: 'jcr', version: '2.0' // Apache 2.0???
    compile group: 'net.java.dev.jna', name: 'jna', version: '4.2.1' // Apache 2.0
    compile group: 'net.java.dev.jna', name: 'jna-platform', version: '4.2.1' // Apache 2.0
    compile group: 'jaxb', name: 'jsr173_1.0_api', version: '' // ???
    compile group: 'org.liquibase', name: 'liquibase-core', version: '3.4.1' // Apache 2.0

    compile group: 'org.commonjava.googlecode.markdown4j', name: 'markdown4j', version: '2.2' // BSD
    compile name: 'org.eclipse.mylyn.wikitext.core_1.8.0.I20121130-0624', version: '' // EPL
    compile name: 'org.eclipse.mylyn.wikitext.confluence.core_1.8.0.I20121130-0624', version: '' // EPL
    compile name: 'org.eclipse.mylyn.wikitext.mediawiki.core_1.8.0.I20121130-0624', version: '' // EPL
    compile name: 'org.eclipse.mylyn.wikitext.textile.core_1.8.0.I20121130-0624', version: '' // EPL
    compile name: 'org.eclipse.mylyn.wikitext.tracwiki.core_1.8.0.I20121130-0624', version: '' // EPL
    compile name: 'org.eclipse.mylyn.wikitext.twiki.core_1.8.0.I20121130-0624', version: '' // EPL

    compile group: 'com.orientechnologies', name: 'orientdb-client', version: '2.1.4' // Apache 2.0
    compile group: 'com.orientechnologies', name: 'orientdb-core', version: '2.1.4' // Apache 2.0
    compile group: 'com.orientechnologies', name: 'orientdb-enterprise', version: '2.1.4' // Apache 2.0
    compile group: 'com.orientechnologies', name: 'orientdb-graphdb', version: '2.1.4' // Apache 2.0
    compile group: 'com.orientechnologies', name: 'orientdb-server', version: '2.1.4' // Apache 2.0
    // runtime group: 'com.orientechnologies', name: 'orientdb-jdbc', version: '2.1.4' // Apache 2.0
    compile group: 'org.xerial.snappy', name: 'snappy-java', version: '1.1.1.7' // ???
    compile group: 'com.tinkerpop.blueprints', name: 'blueprints-core', version: '2.6.0' // BSD
    // compile group: 'com.tinkerpop.blueprints', name: 'blueprints-orient-graph', version: '2.6.0' // BSD
    compile group: 'com.googlecode.concurrentlinkedhashmap', name: 'concurrentlinkedhashmap-lru', version: '1.4.2' // Apache 2.0
    compile group: 'com.tinkerpop.gremlin', name: 'gremlin-java', version: '2.6.0' // BSD
    compile group: 'com.tinkerpop.gremlin', name: 'gremlin-groovy', version: '2.6.0' // BSD
    compile group: 'org.javassist', name: 'javassist', version: '3.20.0-GA' // Apache 2.0

    apiCompile group: 'org.quartz-scheduler', name: 'quartz', version: '2.2.2' // Apache 2.0
    compile group: 'org.quartz-scheduler', name: 'quartz', version: '2.2.2' // Apache 2.0
    compile group: 'org.quartz-scheduler', name: 'quartz-jobs', version: '2.2.2' // Apache 2.0

    compile group: 'org.yaml', name: 'snakeyaml', version: '1.16' // Apache 2.0

    compile group: 'org.apache.ws.commons.util', name: 'ws-commons-util', version: '1.0.2' // Apache 2.0
    compile group: 'org.apache.xmlrpc', name: 'xmlrpc-client', version: '3.1.3' // Apache 2.0
    compile group: 'org.apache.xmlrpc', name: 'xmlrpc-common', version: '3.1.3' // Apache 2.0
    compile group: 'org.apache.xmlrpc', name: 'xmlrpc-server', version: '3.1.3' // Apache 2.0

    compile module(group: 'org.apache.shiro', name: 'shiro-core', version: '1.2.4') { // Apache 2.0
        dependencies module(group: 'commons-beanutils', name: 'commons-beanutils', version: '1.8.3') }
    compile group: 'org.apache.shiro', name: 'shiro-web', version: '1.2.4' // Apache 2.0

    apiCompile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.12'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.12'
    runtime group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.4.1'
    runtime group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.4.1'
    runtime group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.4.1'
    // runtime group: 'org.apache.logging.log4j', name: 'log4j-1.2-api', version: '2.4.1'

    apiCompile group: 'org.intelligents-ia.winstone', name: 'winstone', version: '1.7.0' // CDDL
    // apiCompile group: 'org.intelligents-ia.winstone', name: 'winstone-jndi', version: '1.7.0' // CDDL
    // winstone is in providedCompile so that it won't be included in the war file WEB-INF/lib
    providedCompile group: 'org.intelligents-ia.winstone', name: 'winstone', version: '1.7.0' // CDDL
    // providedCompile group: 'org.intelligents-ia.winstone', name: 'winstone-jndi', version: '1.7.0' // CDDL

    compile module(group: 'org.apache.xmlgraphics', name: 'fop', version: '2.0') { // Apache 2.0
        dependencies module(name: 'avalon-framework', version: '4.2.0'),
                module(group: 'org.apache.xmlgraphics', name: 'xmlgraphics-commons', version: '2.0.1'), // Apache 2.0
                module(group: 'net.sf.barcode4j', name: 'barcode4j', version: '2.1-barcode4j-fop-ext-complete'),
                module(group: 'batik', name: 'batik-all', version: '1.8') // Apache 2.0
    }

    // ElasticSearch Libraries, including Lucene
    apiCompile group: 'org.elasticsearch', name: 'elasticsearch', version: '1.7.3' // Apache 2.0
    compile group: 'org.elasticsearch', name: 'elasticsearch', version: '1.7.3' // Apache 2.0
    compile group: 'org.elasticsearch', name: 'elasticsearch-groovy', version: '1.7.2' // Apache 2.0
    compile group: 'org.apache.lucene', name: 'lucene-analyzers-common', version: '4.10.4' // Apache 2.0
    compile group: 'org.apache.lucene', name: 'lucene-core', version: '4.10.4' // Apache 2.0
    compile group: 'org.apache.lucene', name: 'lucene-grouping', version: '4.10.4' // Apache 2.0
    compile group: 'org.apache.lucene', name: 'lucene-highlighter', version: '4.10.4' // Apache 2.0
    compile group: 'org.apache.lucene', name: 'lucene-join', version: '4.10.4' // Apache 2.0
    compile group: 'org.apache.lucene', name: 'lucene-memory', version: '4.10.4' // Apache 2.0
    compile group: 'org.apache.lucene', name: 'lucene-queries', version: '4.10.4' // Apache 2.0
    compile group: 'org.apache.lucene', name: 'lucene-queryparser', version: '4.10.4' // Apache 2.0
    compile group: 'org.apache.lucene', name: 'lucene-sandbox', version: '4.10.4' // Apache 2.0
    compile group: 'org.apache.lucene', name: 'lucene-suggest', version: '4.10.4' // Apache 2.0
    runtime group: 'com.vividsolutions', name: 'jts', version: '1.13' // LGPL (runtime only, needed by spatial4j)
    runtime group: 'com.spatial4j', name: 'spatial4j', version: '0.5' // Apache 2.0
    runtime group: 'org.apache.lucene', name: 'lucene-spatial', version: '4.10.4' // Apache 2.0

    // Rules Libraries: Drools/jBPM/etc
    apiCompile group: 'org.kie', name: 'kie-api', version: '6.3.0.Final' // Apache 2.0
    compile group: 'org.kie', name: 'kie-api', version: '6.3.0.Final' // Apache 2.0
    compile group: 'org.drools', name: 'drools-compiler', version: '6.3.0.Final' // Apache 2.0
    compile group: 'org.drools', name: 'drools-core', version: '6.3.0.Final' // Apache 2.0
    compile group: 'org.drools', name: 'drools-decisiontables', version: '6.3.0.Final' // Apache 2.0
    compile group: 'org.drools', name: 'drools-templates', version: '6.3.0.Final' // Apache 2.0
    runtime group: 'org.drools', name: 'drools-jsr94', version: '6.3.0.Final' // Apache 2.0
    runtime name: 'jsr94', version: '1.1' // ???
    runtime group: 'org.kie', name: 'kie-internal', version: '6.3.0.Final' // Apache 2.0
    runtime group: 'org.mvel', name: 'mvel2', version: '2.2.7.Final' // Apache 2.0
    runtime group: 'com.google.protobuf', name: 'protobuf-java', version: '2.6.1' // New BSD
    runtime group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.8' // New BSD
    runtime group: 'org.antlr', name: 'antlr-runtime', version: '3.5.2' // New BSD
    runtime group: 'org.apache.poi', name: 'poi', version: '3.13' // Apache 2.0
    runtime group: 'org.apache.poi', name: 'poi-ooxml', version: '3.13' // Apache 2.0
    // really necessary? nearly 5MB file: runtime group: 'org.apache.poi', name: 'poi-ooxml-schemas', version: '3.12' // Apache 2.0
    runtime group: 'org.codehaus.janino', name: 'janino', version: '2.7.8' // New BSD
    runtime group: 'org.codehaus.janino', name: 'commons-compiler', version: '2.7.8' // New BSD
    // NOTE: ecj 4.3.1 is not the latest but is the version distributed with drools/kie 6.2.0.Final
    runtime group: 'org.eclipse.jdt.core.compiler', name: 'ecj', version: '4.4.2' // New BSD

    // ========== runtime-only dependencies ==========
    runtime group: 'org.apache.camel', name: 'camel-csv', version: '2.16.0' // Apache 2.0
    runtime group: 'org.apache.camel', name: 'camel-elasticsearch', version: '2.16.0' // Apache 2.0
    runtime group: 'org.apache.camel', name: 'camel-freemarker', version: '2.16.0' // Apache 2.0
    runtime group: 'org.apache.camel', name: 'camel-groovy', version: '2.16.0' // Apache 2.0
    runtime group: 'org.apache.camel', name: 'camel-http-common', version: '2.16.0' // Apache 2.0
    runtime group: 'org.apache.camel', name: 'camel-http4', version: '2.16.0' // Apache 2.0
    runtime group: 'org.apache.camel', name: 'camel-jms', version: '2.16.0' // Apache 2.0
    runtime group: 'org.apache.camel', name: 'camel-lucene', version: '2.16.0' // Apache 2.0
    runtime group: 'org.apache.camel', name: 'camel-mail', version: '2.16.0' // Apache 2.0
    runtime group: 'org.apache.camel', name: 'camel-mqtt', version: '2.16.0' // Apache 2.0
    runtime group: 'org.apache.camel', name: 'camel-quartz2', version: '2.16.0' // Apache 2.0
    runtime group: 'org.apache.camel', name: 'camel-shiro', version: '2.16.0' // Apache 2.0
    runtime group: 'org.apache.camel', name: 'camel-xmlrpc', version: '2.16.0' // Apache 2.0

    /* For extended WebDAV connection to Jackrabbit these are needed:
    runtime group: 'org.apache.jackrabbit', name: 'jackrabbit-spi', version: '2.5.2' // Apache 2.0
    runtime group: 'org.apache.jackrabbit', name: 'jackrabbit-spi-commons', version: '2.5.2' // Apache 2.0
    runtime group: 'org.apache.jackrabbit', name: 'jackrabbit-jcr2spi', version: '2.5.2' // Apache 2.0
    runtime group: 'org.apache.jackrabbit', name: 'jackrabbit-spi2dav', version: '2.5.2' // Apache 2.0
    runtime group: 'org.apache.jackrabbit', name: 'jackrabbit-webdav', version: '2.5.2' // Apache 2.0
    runtime name: 'commons-httpclient', version: '3.1' // Apache 2.0
    */

    runtime group: 'org.apache.derby', name: 'derby', version: '10.12.1.1' // Apache 2.0
    compile group: 'com.h2database', name: 'h2', version: '1.4.190' // MPL 2.0, EPL 1.0

    runtime group: 'oro', name: 'oro', version: '2.0.8' // Apache 2.0

    // shiro-ehcache is good for caching auth in distributed environments, but has some quirks so not enabled by default (see shiro.ini)
    // runtime name: 'shiro-ehcache', version: '1.2.4' // Apache 2.0
    // runtime name: 'shiro-quartz', version: '1.2.4' // Apache 2.0

    // ========== test dependencies ==========
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.4' // Apache 2.0
    testCompile group: 'junit', name: 'junit', version: '4.12' // Apache 2.0
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3' // BSD 3-Clause
}

task apiJavadoc(type: Javadoc) {
    source sourceSets.api.allJava
    classpath = sourceSets.main.compileClasspath
    destinationDir = file(docsDir.absolutePath + '/apiJavadoc')
}

// setup task dependencies to make sure the start and api sourceSets always get run
compileJava.dependsOn startClasses
compileJava.dependsOn apiClasses
// add the classes directory for the api SourceSet to the main compile classpath so that dependent classes are available
sourceSets.main.compileClasspath += files(sourceSets.api.output.classesDir)

compileTestGroovy.dependsOn apiClasses
compileTestGroovy.dependsOn classes
sourceSets.test.compileClasspath += files(sourceSets.api.output.classesDir)
sourceSets.test.compileClasspath += files(sourceSets.main.output.classesDir)

// by default the Java plugin runs test on build, change to not do that (only run test if explicit task)
check.dependsOn.remove(test)

test {
    systemProperty 'moqui.runtime', '../runtime'
    systemProperty 'moqui.conf', 'conf/MoquiDevTestConf.xml'
    systemProperty 'moqui.init.static', 'true'
    maxHeapSize = "256M"
    jvmArgs '-XX:MaxPermSize=128m'

    testLogging.showStandardStreams = true

    classpath += files(sourceSets.api.output.classesDir)
    classpath += files(sourceSets.main.output.classesDir)
    classpath += files(projectDir.absolutePath)
    //classpath += fileTree(dir: projectDir.absolutePath,
    //        includes: ['data/**', 'entity/**', 'screen/**', 'service/**', 'template/**', 'xsd/**'])

    beforeTest { descriptor ->
        logger.lifecycle("Running test: ${descriptor}")
    }
}

jar {
    // this is necessary otherwise jar won't build when war plugin is applied
    enabled = true
    appendix = 'framework'
    manifest { attributes 'Implementation-Title': 'Moqui Framework', 'Implementation-Version': version }
    from sourceSets.api.output
    from sourceSets.main.output
    // get all of the "resources" that are in component-standard directories instead of src/main/resources
    from fileTree(dir: projectDir.absolutePath,
            includes: ['data/**', 'entity/**', 'screen/**', 'service/**', 'template/**', 'xsd/**'])
}

war {
    dependsOn jar
    // put the war file in the parent directory, ie the moqui dir instead of the framework dir
    destinationDir = file(projectDir.absolutePath + '/..')
    // add MoquiInit.properties to the WEB-INF/classes dir for the deployed war mode of operation
    from(fileTree(dir: projectDir.absolutePath + '/..', includes: ['MoquiInit.properties'])) { into 'WEB-INF/classes' }
    // this excludes the classes in sourceSets.main.output (better to have the jar file built above)
    classpath = configurations.runtime - configurations.providedCompile
    classpath file(jar.archivePath)

    // put start classes and winstone in the root of the war file for the executable war/jar mode of operation
    from sourceSets.start.output
    from file(projectDir.absolutePath + '/lib/winstone-1.7.0.jar')
    from file(projectDir.absolutePath + '/lib/winstone-jndi-1.7.0.jar')
    from file(projectDir.absolutePath + '/lib/slf4j-api-1.7.12.jar')
    from file(projectDir.absolutePath + '/lib/log4j-api-2.4.1.jar')
    from file(projectDir.absolutePath + '/lib/log4j-core-2.4.1.jar')
    from file(projectDir.absolutePath + '/lib/log4j-slf4j-impl-2.4.1.jar')
    from file(projectDir.absolutePath + '/src/main/resources/log4j2.xml')
    from file(projectDir.absolutePath + '/lib/geronimo-servlet_2.5_spec-1.2.jar')
    // setup the manifest for the executable war/jar mode
    manifest { attributes 'Implementation-Title': 'Moqui Start', 'Implementation-Vendor': 'Moqui Framework',
            'Implementation-Version': version, 'Main-Class': 'MoquiStart' }
}
